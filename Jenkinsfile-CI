pipeline {
    agent any  
    options {
        buildDiscarder(logRotator(numToKeepStr: '5'))
    }
    environment {
        WORKSPACE = "${env.WORKSPACE}"
        SERVICES_WITHOUT_TESTS = "spring-petclinic-admin-server spring-petclinic-genai-service"
    }

    stages {
        stage('Checkout Code') {
            steps {
                script {
                    sh '''
                    if [ ! -d spring-petclinic-microservices ]; then
                        git clone https://github.com/22127063/spring-petclinic-microservices.git
                    fi
                    cd spring-petclinic-microservices
                    git reset --hard HEAD
                    git pull origin main
                    '''
                }
            }
        }

        stage('Detect Changes') {
            steps {
                script {
                    echo "Running pipeline for Branch: ${env.BRANCH_NAME}"

                    def prevCommitExists = sh(script: "cd spring-petclinic-microservices && git rev-parse HEAD~1", returnStatus: true) == 0
                    def changedFiles = prevCommitExists 
                        ? sh(script: "cd spring-petclinic-microservices && git diff --name-only HEAD~1 HEAD", returnStdout: true).trim().split("\n")
                        : []

                    def services = [
                        'spring-petclinic-admin-server',
                        'spring-petclinic-api-gateway',
                        'spring-petclinic-config-server',
                        'spring-petclinic-customers-service',
                        'spring-petclinic-discovery-server',
                        'spring-petclinic-genai-service',
                        'spring-petclinic-vets-service',
                        'spring-petclinic-visits-service'
                    ]

                    def changedServiceList = services.findAll { service ->
                        changedFiles.any { it.contains(service) }
                    }

                    def commonFiles = ["pom.xml", ".github", "docker-compose.yml", "Dockerfile"]

                    // ThÃªm táº¥t cáº£ cÃ¡c file báº¯t Ä‘áº§u báº±ng Jenkinsfile-
                    def jenkinsfiles = sh(script: "ls Jenkinsfile-* 2>/dev/null || true", returnStdout: true)
                                    .trim()
                                    .split("\n")
                                    .findAll { it } // loáº¡i bá» chuá»—i rá»—ng náº¿u khÃ´ng cÃ³ file

                    commonFiles += jenkinsfiles

                    if (changedFiles.any { file -> commonFiles.any { file.contains(it) } }) {
                        changedServiceList = services
                    }

                    env.CHANGED_SERVICES = changedServiceList.join(" ")

                    if (env.CHANGED_SERVICES.trim().isEmpty()) {
                        echo "No relevant changes detected. Skipping pipeline."
                        currentBuild.result = 'SUCCESS'
                        return
                    }

                    echo "Services to build: ${env.CHANGED_SERVICES}"
                }
            }
        }
        
        stage('Test Services') {
            when {
                expression { return !env.CHANGED_SERVICES.trim().isEmpty() }
            }
            steps {
                script {
                    def serviceList = env.CHANGED_SERVICES.trim().split(" ")
                    for (service in serviceList) {
                        echo "ðŸ”¬ Testing service: ${service}"
                        dir("spring-petclinic-microservices/${service}") {
                            if (fileExists('pom.xml')) {
                                echo "pom.xml found in ${service}"
                                if (!env.SERVICES_WITHOUT_TESTS.contains(service)) {
                                    try {
                                        sh 'mvn clean test -Dspring.cloud.config.enabled=false'
                                    } catch (Exception e) {
                                        echo "Warning: Tests failed for ${service}, but continuing pipeline"
                                        currentBuild.result = 'UNSTABLE'
                                    }
                                    
                                    junit allowEmptyResults: true, testResults: '**/target/surefire-reports/*.xml'
                                    recordCoverage tools: [[jacoco: '**/target/site/jacoco/jacoco.xml']]
                                } else {
                                    echo "Skipping tests for ${service} (No test folders)"
                                }
                            } else {
                                echo "pom.xml NOT FOUND in ${service}. Skipping tests."
                            }
                        }
                    }
                }
            }
        }

        stage('Check Test Coverage') {
            when {
                expression { !env.CHANGED_SERVICES.trim().isEmpty() && env.BRANCH_NAME != 'main' }
            }
            steps {
                script {
                    def serviceList = env.CHANGED_SERVICES.trim().split(" ")
                    for (service in serviceList) {
                        def servicePath = "spring-petclinic-microservices/${service}/target/site/jacoco/jacoco.csv"
                        if (fileExists(servicePath)) {
                            def coverage = sh(script: "tail -1 ${servicePath} | cut -d',' -f4", returnStdout: true).trim()
                            if (coverage.toInteger() < 70) {
                                error("Test coverage below 70% for ${service}")
                            }
                        } else {
                            error("Coverage report missing for ${service}")
                        }
                    }
                }
            }
        }

        stage('Build Services') {
            when {
                expression { return !env.CHANGED_SERVICES.trim().isEmpty() }
            }
            steps {
                script {
                    def serviceList = env.CHANGED_SERVICES.trim().split(" ")
                    for (service in serviceList) {
                        echo "Building service: ${service}"
                        dir("spring-petclinic-microservices/${service}") {
                            if (fileExists('pom.xml')) {
                                echo "pom.xml found in ${service}, proceeding with build."
                                sh 'mvn package -DskipTests -Dspring.cloud.config.enabled=false'
                                archiveArtifacts artifacts: 'target/*.jar', fingerprint: true
                            } else {
                                echo "pom.xml NOT FOUND in ${service}. Skipping build."
                            }
                        }
                    }
                }
            }
        }

        stage('Docker Build & Push') {
            when {
                expression { return !env.CHANGED_SERVICES?.trim().isEmpty() }
            }
            steps {
                script {
                    def servicesList = env.CHANGED_SERVICES.tokenize(' ')
                    if (servicesList.isEmpty()) {
                        error("âŒ No changed services found. Check Detect Changes stage.")
                    }

                    def servicePorts = [
                        "spring-petclinic-admin-server": 9090,
                        "spring-petclinic-api-gateway": 8090,
                        "spring-petclinic-config-server": 8888,
                        "spring-petclinic-customers-service": 8081,
                        "spring-petclinic-discovery-server": 8761,
                        "spring-petclinic-genai-service": 8084,
                        "spring-petclinic-vets-service": 8083,
                        "spring-petclinic-visits-service": 8082
                    ]

                    def commitHash = sh(script: "cd spring-petclinic-microservices && git rev-parse --short HEAD", returnStdout: true).trim()

                    withCredentials([usernamePassword(
                        credentialsId: 'docker',
                        usernameVariable: 'DOCKERHUB_USER',
                        passwordVariable: 'DOCKERHUB_PASSWORD'
                    )]) {
                        sh "docker login -u ${DOCKERHUB_USER} -p ${DOCKERHUB_PASSWORD}"
                    }

                    for (service in servicesList) {
                        echo "ðŸ³ Building & pushing Docker image for ${service}..."
                        def shortServiceName = service.replaceFirst("spring-petclinic-", "")
                        def servicePort = servicePorts.get(service, 8080)

                        dir("spring-petclinic-microservices") {
                            if (fileExists("Dockerfile")) {
                                def imageTag = "22127063/${service}:${commitHash}"
                                sh """
                                docker build \\
                                    --build-arg SERVICE_NAME=${shortServiceName} \\
                                    --build-arg EXPOSED_PORT=${servicePort} \\
                                    -f Dockerfile \\
                                    -t ${imageTag} \\
                                    -t 22127063/${service}:latest \\
                                    .
                                docker push ${imageTag}
                                docker push 22127063/${service}:latest
                                docker rmi ${imageTag} || true
                                docker rmi 22127063/${service}:latest || true
                                """
                            } else {
                                echo "âŒ Dockerfile not found for ${service}, skipping build."
                            }
                        }
                    }
                }
            }
        }
    }

    post {
        always {
            cleanWs()
        }
    }
}